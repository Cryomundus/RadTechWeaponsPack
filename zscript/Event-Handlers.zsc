//////////////////////////////////////////////////////////////////////
//                                                                  //
//  credits to FDA for coding the RadTech weapon spawner framework  //
//                                                                  //
//////////////////////////////////////////////////////////////////////



// Struct for itemspawn information. 
class RTSpawnItem play
{
	// ID by string for spawner
	string spawnName;
	
	// ID by string for spawnees
	Array<RTSpawnItemEntry> spawnReplaces;
	
	// Whether or not to persistently spawn.
	bool isPersistent;

	// Whether or not to replace the original item.
	bool replaceItem;

	string toString() {

		let replacements = "[";
		if (spawnReplaces.size()) {
			replacements = replacements..spawnReplaces[0].toString();

			foreach (spawnReplace : spawnReplaces) replacements = replacements..", "..spawnReplace.toString();
		}
		replacements = replacements.."]";


		return String.format("{ spawnName=%s, spawnReplaces=%s, isPersistent=%b, replaceItem=%b }", spawnName, replacements, isPersistent, replaceItem);
	}
}

class RTSpawnItemEntry play
{
	string name;
	int    chance;

	string toString() {
		return String.format("{ name=%s, chance=%s }", name, chance >= 0 ? "1/"..(chance + 1) : "never");
	}
}

// Struct for passing useinformation to ammunition. 
class RTSpawnAmmo play
{
	// ID by string for the header ammo.
	string ammoName;
	
	// ID by string for weapons using that ammo.
	Array<string> weaponNames;
	
	string toString() {

		let weapons = "[";
		if (weaponNames.size()) {
			weapons = weapons..weaponNames[0];

			foreach (weaponName : weaponNames) weapons = weapons..", "..weaponName;
		}
		weapons = weapons.."]";

		return String.format("{ ammoName=%s, weaponNames=%s }", ammoName, weapons);
	}
}



// One handler to rule them all. 
class RadTechHandler : EventHandler
{

	// List of persistent classes to completely ignore. 
	// This -should- mean this mod has no performance impact. 
	static const string blacklist[] =
	{
		"HDSmoke",
		"BloodTrail",
		"CheckPuff",
		"WallChunk",
		"HDBulletPuff",
		"HDFireballTail",
		"ReverseImpBallTail",
		"HDSmokeChunk",
		"ShieldSpark",
		"HDFlameRed",
		"HDMasterBlood",
		"PlantBit",
		"HDBulletActor",
		"HDLadderSection"
	};

	// List of weapon-ammo associations.
	// Used for ammo-use association on ammo spawn (happens very often). 
	array<RTSpawnAmmo> ammoSpawnList;
	
	// List of item-spawn associations.
	// used for item-replacement on mapload. 
	array<RTSpawnItem> itemSpawnList;

	bool cvarsAvailable;
	
	// appends an entry to itemSpawnList;
	void additem(string name, Array<RTSpawnItemEntry> replacees, bool persists, bool rep=true)
	{

		if (hd_debug) {
			let msg = "Adding "..(persists ? "Persistent" : "Non-Persistent").." Replacement Entry for "..name..": ["..replacees[0].toString();

			if (replacees.size() > 1) foreach (replacee : replacees) msg = msg..", "..replacee.toString();

			console.printf(msg.."]");
		}

		// Creates a new struct;
		RTSpawnItem spawnee = RTSpawnItem(new('RTSpawnItem'));
		
		// Populates the struct with relevant information,
		spawnee.spawnName = name;
		spawnee.isPersistent = persists;
		spawnee.replaceItem = rep;

		foreach (replacee : replacees) spawnee.spawnReplaces.push(replacee);
		
		// Pushes the finished struct to the array. 
		itemSpawnList.push(spawnee);
	}

	RTSpawnItemEntry addItemEntry(string name, int chance)
	{
		// Creates a new struct;
		RTSpawnItemEntry spawnee = RTSpawnItemEntry(new('RTSpawnItemEntry'));
		spawnee.name = name.makeLower();
		spawnee.chance = chance;
		return spawnee;
	}

	// appends an entry to ammoSpawnList;
	void addAmmo(string name, Array<string> weapons)
	{
	
		// Creates a new struct;
		RTSpawnAmmo spawnee = RTSpawnAmmo(new('RTSpawnAmmo'));
		spawnee.ammoName = name.makeLower();
		
		// Populates the struct with relevant information,
		foreach (weapon : weapons) spawnee.weaponNames.push(weapon.makeLower());
		
		// Pushes the finished struct to the array. 
		ammoSpawnList.push(spawnee);
	}
	

	// Populates the replacement and association arrays. 
	void init()
	{
		cvarsAvailable = true;

		// --------------------
		// Ammo and Magazine Association
		// --------------------
	
		// HDBattery. 
		Array<string> wep_battery;  
		wep_battery.push("PlasmaBuster");
		wep_battery.push("MinervaChaingun");
		addammo('HDBattery', wep_battery);
		
		// 12ga Flare Ammo. 
		Array<string> wep_flare;  
		wep_flare.push("FireBlooper"); 
		wep_flare.push("MetalFireBlooper"); 
		addammo('HDFlareAmmo', wep_flare);	

		// 12 gauge (normal) Ammo. 
		Array<string> wep_12ga;  
		wep_12ga.push("FireBlooper"); 
		wep_12ga.push("MetalFireBlooper");
		wep_12ga.push("HDCombatShotgun");
		wep_12ga.push("DoomHunter");
		addammo('HDShellAmmo', wep_12ga);	
		
		// 12 gauge (less lethal) ammo.
		Array<string> wep_12gall;
		wep_12gall.push("LLHunter");
		addammo("HDLLShellAmmo", wep_12gall);

		// 12 gauge (explosive) ammo.
		Array<string> wep_12gaex;
		wep_12gaex.push("ExplosiveHunter");
		wep_12gaex.push("MetalFireBlooper");
		addammo("HDExplosiveShellAmmo", wep_12gaex);
				
		// 10mm ammo
		Array<string> wep_10mm;  
		wep_10mm.push("HD10mmPistol"); 
		wep_10mm.push("HDSigcow");
		wep_10mm.push("TenMilAutoReloadingThingy");
		addammo ('HD10mAmmo', wep_10mm);

		// 10mm ammo (brass)
		Array<string> wep_10mmbrass;  
		wep_10mmbrass.push("TenMilAutoReloadingThingy");
		addammo ('TenMilBrass', wep_10mmbrass);

		// 4mm 'volt caseless'
		Array<string> wep_4mmvolt;
		wep_4mmvolt.push("HackedZM66AssaultRifle");
		addammo('FourMilAmmo', wep_4mmvolt);	
		
		// Volt 4mm Magazines
		Array<string> wep_4mmvolt50;
		wep_4mmvolt50.push("HackedZM66AssaultRifle");
		addammo('HD4mMag', wep_4mmvolt50);
			
		// Rocket (Gyro) Grenades.
		Array<string> wep_rocket;
		wep_rocket.push("HackedZM66AssaultRifle");
		addammo('HDRocketAmmo', wep_rocket);

		// 9mm
		Array<string> wep_9mm;
		wep_9mm.push("MinervaChaingun");
		wep_9mm.push("HDSnubNoseRevolver");
		wep_9mm.push("TenMilAutoReloadingThingy");
		wep_9mm.push("HDStenMk2");
		wep_9mm.push("HushPuppyPistol");
		wep_9mm.push("HDHorseshoePistol");
		addammo('HDPistolAmmo', wep_9mm);
		
		// 9mm pistol magazines
		Array<string> wep_9mm15;
		wep_9mm15.push("HushPuppyPistol");
		wep_9mm15.push("HDHorseshoePistol");
		addammo('HD9mMag15', wep_9mm15);
		
		// 9mm smg magazines
		Array<string> wep_9mm30;
		wep_9mm30.push("MinervaChaingun");
		wep_9mm30.push("HDStenMk2");
		addammo('HD9mMag30', wep_9mm30);

		// 355
		Array<string> wep_355;
		wep_355.push("HDSnubNoseRevolver");
		addammo('HDRevolverAmmo', wep_355);

		// 45 ACP
		Array<string> wep_45acp;
		wep_45acp.push("HDColt1911");
		addammo('HD45ACPAmmo', wep_45acp);

		// 45 Long Colt
		Array<string> wep_45lc;
		wep_45lc.push("HDSingleActionRevolver");
		addammo('HD45LCAmmo', wep_45lc);

		// 45 Long Colt (gold)
		Array<string> wep_45lcgold;
		wep_45lcgold.push("HDGoldenSingleActionRevolver");
		addammo('HDGold45LCAmmo', wep_45lcgold);

		// 7mm Clips
		Array<string> wep_7mmClip;
		wep_7mmClip.push("ObrozzPistol");
		addammo('HD7mClip', wep_7mmClip);

		// 7mm
		Array<string> wep_7mm;
		wep_7mm.push("ObrozzPistol");
		addammo('SevenMilAmmo', wep_7mm);

		// 7mm Recast
		Array<string> wep_7mmR;
		wep_7mmR.push("ObrozzPistol");
		addammo('SevenMilAmmoRecast', wep_7mmR);

		// --------------------
		// Weapon/Item Spawns
		// --------------------

        // Note:
        // Anything that replaces items directly must have persistent 
        // spawns disabled to avoid them getting "transmogrified" in 
        // weird ways, like mags ejected when firing/reloading, or 
        // dumping the contents of a backpack.
        //
        // I changed most of the weapon spawners to replace ammoboxes and
        // spawn mags on shell pickups. I couldn't find any decent way to
        // spawn items on clip/mag spawns, so this is the best I could do.
        //
        //
        // -- Swampie
        
    // ----- Energy Weapons -----
        
		// Stun Gun
		Array<RTSpawnItemEntry> spawns_stungun;
		spawns_stungun.push(additementry('Lumberjack', stungun_spawn_bias));
		additem('HDStunGun', spawns_stungun, false);

		// Phazer Plasma Pistol
		Array<RTSpawnItemEntry> spawns_phazer;  
		spawns_phazer.push(additementry('HDAmBoxUnarmed', phazer_spawn_bias));
		additem('PhazerPistol', spawns_phazer, phazer_persistent_spawning);

		
		// Micro-Cells
		Array<RTSpawnItemEntry> spawns_microcell;
		spawns_microcell.push(additementry('ShellPickup', microcell_spawn_bias));
	    spawns_microcell.push(additementry('HDIEDPack', microcell_spawn_bias));
        spawns_microcell.push(additementry('HDIEDKits', microcell_spawn_bias));
		additem('HDMicroCell', spawns_microcell, microcell_persistent_spawning);

		// DM-93 Plasma Rifle
		Array<RTSpawnItemEntry> spawns_plasmabuster;  
		spawns_plasmabuster.push(additementry('Thunderbuster', pbuster_cpack_spawn_bias)); 
		additem('PlasmaBuster', spawns_plasmabuster, false);

    // ----- Pistols -----
        
		// Hush Puppy Silenced Pistol
		Array<RTSpawnItemEntry> spawns_hushpuppy;  
		spawns_HushPuppy.push(additementry('HDAmBoxUnarmed', hushpuppy_spawn_bias)); 
	    spawns_HushPuppy.push(additementry('HDAmBox', hushpuppy_spawn_bias)); 
		additem('HushPuppyPistol', spawns_hushpuppy,  hushpuppy_persistent_spawning);


		// Colt 1911 .45 Pistol
	//	Array<RTSpawnItemEntry> spawns_colt1911;  
	//	spawns_colt1911.push(additementry('HunterRandom', colt1911_spawn_bias)); 
	//	spawns_colt1911.push(additementry('HDPistol', colt1911_spawn_bias)); 
	//	additem('HDColt1911', spawns_colt1911,  colt1911_persistent_spawning);
	    
		// Colt 1911 Mags
		Array<RTSpawnItemEntry> spawns_colt1911_mags;
		spawns_colt1911_mags.push(additementry('ShellPickup', colt1911_mags_spawn_bias));
        spawns_colt1911_mags.push(additementry('HDIEDPack', colt1911_mags_spawn_bias));
        spawns_colt1911_mags.push(additementry('HDIEDKits', colt1911_mags_spawn_bias));
		additem('HDColtMag7', spawns_colt1911_mags, colt1911_persistent_spawning);
			    
		//Random ammobox spawns
		Array<RTSpawnItemEntry> spawns_colt1911_ammobox;
		spawns_colt1911_ammobox.push(additementry('HDAmBoxUnarmed', colt1911_ammobox_spawn_bias));
		spawns_colt1911_ammobox.push(additementry('HDAmBox', colt1911_ammobox_spawn_bias));
		additem('HDColt1911Spawn', spawns_colt1911_ammobox, colt1911_persistent_spawning);


		// Delta Elite spawns
		Array<RTSpawnItemEntry> spawns_10mm_pistol;
		spawns_10mm_pistol.push(additementry('HDAmBoxUnarmed', tenpis_handgun_spawn_bias));
        spawns_10mm_pistol.push(additementry('HDAmBox', tenpis_handgun_spawn_bias));
		additem('HD10mmPistol', spawns_10mm_pistol, tenpis_persistent_spawning);
		
		// Delta Elite Magazines
		Array<RTSpawnItemEntry> spawns_10mm_pistol_mags;
		spawns_10mm_pistol_mags.push(additementry('ShellPickup', tenpismag_cmag_spawn_bias));
		spawns_10mm_pistol_mags.push(additementry('HDIEDPack', tenpismag_cmag_spawn_bias));
        spawns_10mm_pistol_mags.push(additementry('HDIEDKits', tenpismag_cmag_spawn_bias));
		additem('HD10mMag8', spawns_10mm_pistol_mags, tenpis_persistent_spawning);
        

		// Obrozz Sawn-Off Boss Pistol
		Array<RTSpawnItemEntry> spawns_obrozz;
		spawns_obrozz.push(additementry('HDAmBoxUnarmed', obrozz_spawn_bias));
	    spawns_obrozz.push(additementry('HDAmBox', obrozz_spawn_bias));
		additem('ObrozzSpawner', spawns_obrozz, obrozz_persistent_spawning);
        

    // --- Revolvers ---

		// Detective Special
		Array<RTSpawnItemEntry> spawns_snose_pistol;
		spawns_snose_pistol.push(additementry('DeinoSpawn', snose_pistol_spawn_bias));	
		additem('SnubNoseSpawn', spawns_snose_pistol, snose_persistent_spawning);


		// Single-Action Revolver
	//	Array<RTSpawnItemEntry> spawns_sixgun;
	//	spawns_sixgun.push(additementry('HDHandgunRandomDrop', sa_pistol_spawn_bias));
	//	spawns_sixgun.push(additementry('HDPistol', sa_pistol_spawn_bias));
	//	additem('HDSingleActionRevolver', spawns_sixgun, sa_persistent_spawning);
        
        Array<RTSpawnItemEntry> spawns_sixgun_bundle;
		spawns_sixgun_bundle.push(additementry('DeinoSpawn', sa_bundle_spawn_bias));
		additem('SADeinoSpawn', spawns_sixgun_bundle, sa_persistent_spawning);


		// Gold Single-Action
		Array<RTSpawnItemEntry> spawns_goldengun;
		spawns_goldengun.push(additementry('DeinoSpawn', gsa_pistol_spawn_bias));
		additem('GoldSADeinoSpawn', spawns_goldengun, gsa_persistent_spawning);


    // ----- Shotguns -----

		// Ithaca M37 Combat Shotgun
		Array<RTSpawnItemEntry> spawns_combatshotgun;
		spawns_combatshotgun.push(additementry('HunterRandom', cshotgun_shotgun_spawn_bias));
		additem('HDCombatShotgunRandom', spawns_combatshotgun, cshotgun_persistent_spawning);


		// Doomed Shotgun
		Array<RTSpawnItemEntry> spawns_doomedshotgun;
		spawns_doomedshotgun.push(additementry('HunterRandom', dHunt_shotgun_spawn_bias));
		additem('DoomHunterRandom', spawns_doomedshotgun, dHunt_persistent_spawning);


		// Less-Lethal Shotgun
		Array<RTSpawnItemEntry> spawns_llshotgun;
		spawns_llshotgun.push(additementry('HunterRandom', llh_shotgun_spawn_bias));
		additem('LLHunter', spawns_llshotgun, llh_persistent_spawning);


		// Explosive Shotgun
		Array<RTSpawnItemEntry> spawns_exshotgun;
		spawns_exshotgun.push(additementry('HunterRandom', esg_shotgun_spawn_bias));
		additem('ExplosiveHunter', spawns_exshotgun, esg_persistent_spawning);


		// Plastic Flaregun
		Array<RTSpawnItemEntry> spawns_flaregun_plastic; 
		spawns_flaregun_plastic.push(additementry("Hunter", fl_weapon_spawn_bias)); 
		spawns_flaregun_plastic.push(additementry("Slayer", fl_weapon_spawn_bias)); 
		additem("WildFlareGun", spawns_flaregun_plastic, fl_weapon_persistent_spawning, false); 

		// Metal Flaregun
		Array<RTSpawnItemEntry> spawns_flaregun_metal; 
		spawns_flaregun_metal.push(additementry("Hunter", fl_metal_spawn_bias));
		spawns_flaregun_metal.push(additementry("Slayer", fl_metal_spawn_bias));
		additem("WildMetalFlareGun", spawns_flaregun_metal, fl_metal_persistent_spawning, false);


    // ----- Automatic Weapons -----

		// M-211 Sigcow
		Array<RTSpawnItemEntry> spawns_sigcow;
		spawns_sigcow.push(additementry('HDAmBoxUnarmed', sigcow_cbox_spawn_bias));
        spawns_sigcow.push(additementry('HDAmBox', sigcow_cbox_spawn_bias));
		additem('SigCowRandomSpawn', spawns_sigcow, sigcow_persistent_spawning);
		
		// Sigcow Magazines
		Array<RTSpawnItemEntry> spawns_sigcow_mags;
		spawns_sigcow_mags.push(additementry('ShellPickup', sigcow_cbox_spawn_bias));
        spawns_sigcow_mags.push(additementry('HDIEDPack', sigcow_cbox_spawn_bias));
        spawns_sigcow_mags.push(additementry('HDIEDKits', sigcow_cbox_spawn_bias));
		additem('HD10mMag25', spawns_sigcow_mags, sigcow_persistent_spawning);

		// Hacked ZM66
		Array<RTSpawnItemEntry> spawns_hackedzm66random;
		spawns_hackedzm66random.push(additementry('HDAmBoxUnarmed', hacked_zm66_spawn_bias));
        spawns_hackedzm66random.push(additementry('HDAmBox', hacked_zm66_spawn_bias));
		additem('HackedZM66Random', spawns_hackedzm66random, hacked_persistent_spawning);


		// STEN Mk. 2(S)
		Array<RTSpawnItemEntry> spawns_sten;  
		spawns_sten.push(additementry('HDSMGRandom', sten_spawn_bias));
		spawns_sten.push(additementry('HDAmBoxUnarmed', sten_ammobox_spawn_bias));
        spawns_sten.push(additementry('HDAmBox', sten_ammobox_spawn_bias));
		additem('HDSTENRandom', spawns_sten,  sten_persistent_spawning);  


		// Minerva
		Array<RTSpawnItemEntry> spawns_minerva;
		spawns_minerva.push(additementry('Vulcanette', minerva_chaingun_spawn_bias));
		additem('MinervaRandom', spawns_minerva, false);
        
        Array<RTSpawnItemEntry> spawns_minerva_smg;
        spawns_minerva_smg.push(additementry('HDSMGRandom', minerva_smg_spawn_bias));
		additem('MinervaRandom', spawns_minerva_smg, minerva_persistent_spawning);


    // ----- Explosives -----

		// Single bits of Dynamite
		Array<RTSpawnItemEntry> spawns_dynamite;  
		spawns_dynamite.push(additementry('HDRocketAmmo', dynamite_spawn_bias)); 
		spawns_dynamite.push(additementry('HDFragP', dynamite_spawn_bias)); 
		additem('HDDynamiteAmmo', spawns_dynamite, false);
	    
	    
	    // Dynamite Packs
		Array<RTSpawnItemEntry> spawns_dynamitebundle;  
		spawns_dynamitebundle.push(additementry('RocketBigPickup', dynamite_spawn_bias)); 
		additem('HDDynamitePickup', spawns_dynamitebundle, dynamite_persistent_spawning);


		// Frag Cannon
		Array<RTSpawnItemEntry> spawns_fragcannon;  
		spawns_fragcannon.push(additementry('RocketBigPickup', fragcannon_spawn_bias));
		additem('FragCannonPickup', spawns_fragcannon, fragcannon_persistent_spawning);


/*  tried something, didn't work, maybe fix later??

		// 'Juan' Horseshoe Pistol
		Array<RTSpawnItemEntry> spawns_juan;
		spawns_juan.push(additementry('HDHandgunRandomDrop', ja_weapon_drop_chance_bias));
		spawns_juan.push(additementry('HDPistol', ja_weapon_drop_chance_bias));
		additem('HDHorseShoePistol', spawns_juan, ja_persistent_spawning);
*/

	}
	
	// Random stuff, stores it and forces negative values just to be 0.
	bool giveRandom(int chance)
	{
		if (chance > -1)
		{
			let result = random(0, chance);

			if (hd_debug) console.printf("Rolled a "..result.." out of "..(chance + 1));

			return result == 0;
		}
		
		return false;
	}

	// Tries to create the item via random spawning.
	bool tryCreateItem(Actor thing, string spawnName, int chance, bool rep)
	{
		if (giveRandom(chance))
		{
			if (Actor.Spawn(spawnName, thing.pos) && rep)
			{
				if (hd_debug) console.printf(thing.getClassName().." -> "..spawnName);
				
				thing.destroy();
				
				return true;
			}
		}

		return false;
	}
	
	override void worldThingSpawned(WorldEvent e)
	{
		// Populates the main arrays if they haven't been already. 
		if (!cvarsAvailable) init();

		// If thing spawned doesn't exist, quit
		if (!e.thing) return;
		
		// If thing spawned is blacklisted, quit
		foreach (bl : blacklist) if (e.thing is bl) return;

		string candidateName = e.thing.getClassName();
		candidateName = candidateName.makeLower();
		
		// Pointers for specific classes.
		let ammo = HDAmmo(e.thing);
		
		// If the thing spawned is an ammunition, add any and all items that can use this.
		if (ammo) handleAmmoUses(ammo, candidateName);
		
		//Do this AFTER pushing ItemsThatUseThis()
		//or ammo purging gets messed up lol
		
		// Return if range before replacing things.
        if (level.MapName ~== "RANGE") return;
				
		handleWeaponReplacements(e.thing, ammo, candidateName);
	}

	private void handleAmmoUses(HDAmmo ammo, string candidateName)
	{
		foreach (ammoSpawn : ammoSpawnList) if (candidateName == ammoSpawn.ammoName) ammo.itemsThatUseThis.copy(ammoSpawn.weaponNames);
	}

	private void handleWeaponReplacements(Actor thing, HDAmmo ammo, string candidateName)
	{
		// Checks if the level has been loaded more than 1 tic.
		bool prespawn = !(level.maptime > 1);

		// Iterates through the list of item candidates for e.thing.
		foreach (itemSpawn : itemSpawnList)
		{
			// if an item is owned or is an ammo (doesn't retain owner ptr), 
			// do not replace it. 
			let item = Inventory(thing);
			if ((prespawn || itemSpawn.isPersistent) && (!(item && item.owner) && (!ammo || prespawn)))
			{
				foreach (spawnReplace : itemSpawn.spawnReplaces)
				{
					if (spawnReplace.name == candidateName)
					{
						if (hd_debug) console.printf("Attempting to replace "..candidateName.." with "..itemSpawn.spawnName.."...");

						if (tryCreateItem(thing, itemSpawn.spawnName, spawnReplace.chance, itemSpawn.replaceItem)) return;
					}
				}
			}
		}
	}
}

// Juan Handler

class JuanEvent : EventHandler
{
	private bool cvarsAvailable;
	private int weaponSpawnBiasActual;
	private int magSpawnBiasActual;
	private int autoSpawnBiasActual;
	private int weaponDropBiasActual;

	// Shoves cvar values into their non-cvar shaped holes.
	// I have no idea why names for cvars become reserved here.
	// But, this works. So no complaints. 
	void init()
	{
		cvarsAvailable = true;
		weaponSpawnBiasActual = ja_weapon_spawn_bias;
		magSpawnBiasActual    = ja_mag_spawn_bias;
		autoSpawnBiasActual   = ja_firemode_chance_bias;
		weaponDropBiasActual  = ja_weapon_drop_chance_bias;
	}

	// 'Initalizes' the event handler,
	// In my testing, this is called after events are fired. 
	override void WorldLoaded(WorldEvent e)
	{
		// always calls init.
		init();
		super.WorldLoaded(e);
	}

	bool giverandom(int chance)
	{
		bool result = false;
		
		// temp storage for the random value. 
		int iii = random(0, chance);
		
		// force negative values to be 0. 
		if (iii < 0)
			iii = 0;
			
		
		if (iii == 0)
		{
			if (chance > -1)
				result = true;
		}
		
		return result;
	}

	// Handles 'item propegation'. Woo, fancy. 
	override void CheckReplacement(ReplaceEvent e)
	{
		// Makes sure the values are always loaded before
		// taking in replaceevents. 
		if (!cvarsAvailable)
			init();
		
		// Don't replace if it's final, or doesn't exist. 
		if (!e.Replacement || e.IsFinal)
		{
			return;
		}
		
		switch(e.Replacement.GetClassName())
		{
			case 'HDHandgunRandomDrop':
			case 'PepperPistolReplacer':
				// dirty inverted logic gate for spawn drops. 
				if (random(0, weaponDropBiasActual) || weaponDropBiasActual == -1)
				{
					break;
				}
			case 'HDPistol':
				{
					
					if (giverandom(weaponSpawnBiasActual))
					{
						if (giverandom(autoSpawnBiasActual))
						{
							e.Replacement = "HDHorseShoePistolAuto";
						}
						else
						{
							e.Replacement = "HDHorseShoePistol";
						}
					}
					break;
				}
			case 'HD9mMag15':
				if (giverandom(magSpawnBiasActual))
				{
					e.Replacement = "HDHorseshoe9m"; 
				}
				break;
		}
		
	}
}

// Wild flaregun spawns; mostly self explanatory. 
class WildFlareGun : IdleDummy
{
	class<hdweapon> weaponToSpawn;
	property weaponToSpawn : weaponToSpawn;
	
	default
	{
		wildflaregun.weaponToSpawn 'FireBlooper';
	}
	
	states
	{
		Spawn:
			TNT1 A 0 NODELAY
			{
				if (weaponToSpawn != "none")
				{
					let weapon = Actor.Spawn(invoker.weaponToSpawn, (invoker.pos.x, invoker.pos.y, invoker.pos.z + 5));
					weapon.vel.x += frandom[huminahumina](-2,2);
					weapon.vel.y += frandom[huminahumina](-2,2);
					weapon.vel.z += frandom[huminahumina](1,2);
				}
				
				let ammo = HDFlareAmmo(Actor.Spawn("HDFlareAmmo", (invoker.pos.x, invoker.pos.y, invoker.pos.z + 5)));
				ammo.vel.x += frandom[huminahumina](-2,2);
				ammo.vel.y += frandom[huminahumina](-2,2);
				ammo.vel.z += frandom[huminahumina](1,2);
				ammo.amount += random(2,5);
			}
			stop;
	}
}

class WildMetalFlareGun : WildFlareGun
{
	
	default
	{
		wildflaregun.weaponToSpawn 'MetalFireBlooper';
	}
}

// only spawns the ammo (used for spawning ontop of shellpickups). 
class HDFlareAmmoRandom : WildFlareGun
{
	
	default
	{
		wildflaregun.weaponToSpawn "none";
	}
}
